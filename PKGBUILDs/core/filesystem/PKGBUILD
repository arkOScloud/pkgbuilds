# $Id$
# Maintainer: Tom Gundersen <teg@jklm.no>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - modifed os-release

# Maintenance contact:
# arkOS: Jacob Cook <jacob@citizenweb.is>
#  - added manpage for arkOS

noautobuild=1

pkgname=filesystem
pkgver=2013.06
pkgrel=1
pkgdesc='Base filesystem'
arch=('i686' 'x86_64')
license=('GPL')
url='http://www.arkos.io'
groups=('base')
install='filesystem.install'
makedepends=('asciidoc')
depends=('iana-etc')
backup=('etc/fstab' 'etc/crypttab' 'etc/group' 'etc/hosts' 'etc/ld.so.conf' 'etc/passwd'
        'etc/shadow' 'etc/gshadow' 'etc/resolv.conf' 'etc/motd' 'etc/nsswitch.conf'
        'etc/shells' 'etc/host.conf' 'etc/securetty' 'etc/profile' 'etc/issue')
source=('group' 'issue' 'nsswitch.conf' 'securetty' 'host.conf' 'ld.so.conf'
        'passwd' 'shadow' 'fstab' 'crypttab' 'hosts' 'motd' 'os-release' 'resolv.conf'
        'shells' 'gshadow' 'profile' 'modprobe.d.usb-load-ehci-first' 'arkos.7.txt'
	'locale.sh')
md5sums=('7b8e40f8f793e60f471511641bb1c9f2'
         '9791212e79b6747dd72cd4e0baa2a3dd'
         '9e4533df61f0c82d6b2e2371f7376282'
         '4c4540eeb748bf1f71d631b8c1dcf0b3'
         'f28150d4c0b22a017be51b9f7f9977ed'
         '6e488ffecc8ba142c0cf7e2d7aeb832e'
         '1cfd310dff0804a7d21958487c32a3b1'
         '2ef2429c2c98d0883906aa6f5363be80'
         '35b0a161af6fb31e09e9ed16ced18381'
         '237583a96187905bf78db86987c30317'
         'a4d6818082b72b087ee1d9407ea6604e'
         'd41d8cd98f00b204e9800998ecf8427e'
         '1de6cb09519de8891ed0bdade2fa5d7a'
         '6f48288b6fcaf0065fcb7b0e525413e0'
         '22518e922891f9359f971f4f5b4e793c'
         'f45389e116ab0b9e9968287617d27c7d'
         'da66f17c7c766c006d9e341cf43ae4ac'
         'a8a962370cd0128465d514e6a1f74130'
         '18a2cc35ec4b6344414947d4d1da47b1'
         'bb96e7e7f7d1598acbc15d1a79691429')

build() {
	cd ${srcdir}
	a2x -d manpage -f manpage arkos.7.txt
}

package() {
	cd ${pkgdir}

	#
	# setup root filesystem
	#
	for d in boot dev etc home mnt usr var opt srv/http run; do
		install -d -m755 ${d}
	done
	install -d -m555 proc
	install -d -m555 sys
	install -d -m0750 root
	install -d -m1777 tmp
	# vsftpd won't run with write perms on /srv/ftp
	install -d -m555 -g ftp srv/ftp

	# setup /etc
	install -d etc/{ld.so.conf.d,skel,profile.d}
	for f in fstab group host.conf hosts issue ld.so.conf motd nsswitch.conf os-release passwd resolv.conf securetty shells profile; do
		install -m644 ${srcdir}/${f} etc/
	done
	ln -s /proc/self/mounts etc/mtab
	for f in gshadow shadow crypttab; do
		install -m600 ${srcdir}/${f} etc/
	done
	touch etc/arch-release
	install -D -m644 ${srcdir}/modprobe.d.usb-load-ehci-first usr/lib/modprobe.d/usb-load-ehci-first.conf
	install -m755 ${srcdir}/locale.sh etc/profile.d/locale.sh

	# setup /var
	for d in cache/man local opt log/old lib/misc empty; do
		install -d -m755 var/${d}
	done
	install -d -m1777 var/{tmp,spool/mail}
	# allow setgid games to write scores
	install -d -m775 -g games var/games
	ln -s spool/mail var/mail
	ln -s ../run var/run
	ln -s ../run/lock var/lock

	#
	# setup /usr hierarchy
	#
	for d in bin include lib share/misc src; do 
		install -d -m755 usr/${d}
	done
	for d in $(seq 8); do
		install -d -m755 usr/share/man/man${d}
	done

 	#
	# add lib symlinks
	#
	ln -s usr/lib ${pkgdir}/lib
	[[ ${CARCH} = "x86_64" ]] && (
		ln -s usr/lib ${pkgdir}/lib64
		ln -s lib ${pkgdir}/usr/lib64
	)

	#
	# add bin symlinks
  	#
  	ln -s usr/bin ${pkgdir}/bin
  	ln -s usr/bin ${pkgdir}/sbin
  	ln -s bin ${pkgdir}/usr/sbin

	#
	# install arkos(7) manpage
	#
	install -D -m644 ${srcdir}/arkos.7 usr/share/man/man7/arkos.7

	#
	# setup /usr/local hierarchy
	#
	for d in bin etc games include lib man sbin share src; do
		install -d -m755 usr/local/${d}
	done
	ln -s ../man usr/local/share/man
}
