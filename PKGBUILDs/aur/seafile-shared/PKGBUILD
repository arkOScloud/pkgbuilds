# Maintainer: Moritz Maxeiner <moritz@ucworks.org>

pkgname=seafile-shared
pkgver=3.0.2
pkgrel=2
pkgdesc="Shared components of seafile: seafile-daemon, libseafile, libseafile python bindings, manuals, and icons"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="https://github.com/haiwen/seafile/"
license=('GPL')
depends=('ccnet>=1.4.2' 'fuse')
makedepends=('vala' 'intltool' 'fuse')
optdepends=()
options=('!libtool' '!emptydirs')
source=("https://github.com/haiwen/seafile/archive/v${pkgver}.tar.gz"
        "0002-Add-autoconfiguration-for-libjansson.patch")

prepare () {
        cd "$srcdir/seafile-${pkgver}"
        patch -p1 -i $srcdir/0002-Add-autoconfiguration-for-libjansson.patch
}


build ()
{
	cd "$srcdir/seafile-${pkgver}"
	./autogen.sh
	./configure --disable-server --disable-console --prefix=/usr PYTHON=/usr/bin/python2
	make -j1
}

package ()
{
	cd "${srcdir}/seafile-${pkgver}"
	make DESTDIR="${pkgdir}/" install

	# Remove seafile binary
	rm -rf "${pkgdir}/usr/bin/seafile"
	# Remove cli client binary
	rm -rf "${pkgdir}/usr/bin/seaf-cli"

	install -D -m644 ${srcdir}/seafile-${pkgver}/LICENCE.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENCE.txt
}
sha256sums=('85fdb8074508fe918559bbf6b7d6b0062768d65322f6c057080d7e90275094ba'
            '49cdcce7e6a0a8fd1da9cb14e20abd41b0fb9b62df2d7d3947a33f054a772cfe')
